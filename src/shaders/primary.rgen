#version 460
#extension GL_NV_ray_tracing : require
#extension GL_GOOGLE_include_directive : require

#include "Defines.glsl"
#include "Camera.glsl"
#include "DataLayouts.glsl"
#include "Sample.glsl"
#include "Sphere.glsl"

layout(set = 0, binding = ACCELERATION_STRUCTURE_NV_BINDING_LOCATION) uniform accelerationStructureNV scene;
layout(set = 0, binding = RESULT_IMAGE_BINDING_LOCATION, rgba8) uniform image2D resultImage;
layout(set = 0, binding = OCCLUSION_IMAGE_BINDING_LOCATION, rgba32f) uniform image2D shadowImage;
layout(set = 0, binding = CAMERA_BUFFER_BINDING_LOCATION, std140) uniform cameraBuffer
{
	CameraShader camera;
};
layout(set = 0, binding = LIGHTS_BUFFER_BINDING_LOCATION, std140) readonly buffer lightBuffer
{
	SphericalLightSource lights[];
};
layout(set = 0, binding = OTHER_DATA_BUFFER_BINDING_LOCATION, std140) uniform otherDataBuffer
{
	OtherData otherData;
};

layout(location = PRIMARY_PAYLOAD_LOCATION) rayPayloadNV PrimaryRayPayload primaryPayload;
layout(location = SECONDARY_PAYLOAD_LOCATION) rayPayloadNV ShadowRayPayload secondaryPayload;

// Tried linear and power - didn't work well
float PolynomialAOAttenuation(float dist)
{
	// Convert from range [0, MAX_OCCLUSION_DIST] to [0, 1]
	float x = min(dist, MAX_OCCLUSION_DIST) / MAX_OCCLUSION_DIST;
	
	// Function: y = POLYNONIAL_ATTENUATION_Y_MAX - x^POLYNOMIAL_ATTENUATION_POWER
	return POLYNONIAL_AO_ATTENUATION_Y_MAX - pow(x, POLYNOMIAL_AO_ATTENUATION_POWER);
}

void main()
{
	const uint rayFlags = gl_RayFlagsNoneNV;
    const uint cullMask = 0xFF;
    const uint sbtRecordStride = 0;
    const float tMin = 0.0f;
    const float tMax = 1000.0f;
	
	// Trace primary ray
	Ray ray = GenerateRayFromCamera(camera);
    traceNV(scene, rayFlags, cullMask, PRIMARY_CHIT_IDX, sbtRecordStride, PRIMARY_MISS_IDX, ray.origin, tMin, ray.dir, tMax, PRIMARY_PAYLOAD_LOCATION);
    
    // Early exit - no geometry hit
	if (primaryPayload.normalAndHitDistance.w < 0.0f)
	{
		//Background color: blue sky-ish
		const float y = gl_LaunchIDNV.y / 1024.0f;
		const float red = mix(0.0f, 0.7f, y);
		const float green = mix(0.65f, 0.9f, y);
		imageStore(resultImage, ivec2(gl_LaunchIDNV.xy), vec4(0.8f, green, red, 1.0f));
#if AO_CONE
		imageStore(shadowImage, ivec2(gl_LaunchIDNV.xy), vec4(vec3(1.0f), 1.0f));
#elif AO_HEMISPHERE
		imageStore(shadowImage, ivec2(gl_LaunchIDNV.xy), vec4(vec3(DEFAULT_OCCLUSION), 1.0f));
#endif
		return;
	}
	
	// Intersection data
	vec3 isectPoint = ray.origin + (ray.dir * primaryPayload.normalAndHitDistance.w);
	vec3 isectNormal = primaryPayload.normalAndHitDistance.xyz;
	
#if AO_CONE	
#define TIGHTEN 0.35f
	int numOcclusionSteps = 6;
	int totalOcclusionSamples = numOcclusionSteps * numOcclusionSteps;
	int totalOcclusionSamplesPerLight = totalOcclusionSamples / otherData.numSphericalLightSources;
	int numOcclusionStepsPerLighti = int(sqrt(totalOcclusionSamplesPerLight));
	int numOcclusionStepsPerLightj = numOcclusionStepsPerLighti;
	int totalOcclusionSamplesPerLightGoingUp = numOcclusionStepsPerLighti * numOcclusionStepsPerLightj;
	int remainingSamplesPerLight = totalOcclusionSamplesPerLight - totalOcclusionSamplesPerLightGoingUp;
	numOcclusionStepsPerLighti += remainingSamplesPerLight / numOcclusionStepsPerLighti;
	int totalOcclusionStepsPerLight = numOcclusionStepsPerLighti * numOcclusionStepsPerLightj;
	int totalOcclusionSteps = totalOcclusionStepsPerLight * otherData.numSphericalLightSources;
	vec3 color = vec3(0.0f);
	vec3 shadow = vec3(0.0f);
	const vec2 lerpMin = vec2(TIGHTEN);
	const vec2 lerpMax = vec2(1.0f - TIGHTEN);
	for (int l = 0; l < otherData.numSphericalLightSources; l++)
	{
		// Extract light data
		vec3 lightCenter = lights[l].centerAndRadius.xyz;
		float lightRadius = lights[l].centerAndRadius.w;
		vec3 lightEmittance = lights[l].emittance.rgb;
		
		// Sample a cone of directions given the vector from the isect point to the light and its radius
		vec3 isectPointToLightCenter = lightCenter - isectPoint;
		vec3 isectPointToLightCenterDir = normalize(isectPointToLightCenter);
		float isectPointToLightCenterDist = length(isectPointToLightCenter);
		float tangentDist = sqrt((isectPointToLightCenterDist * isectPointToLightCenterDist) - (lightRadius * lightRadius));
		float cosThetaIsectPointLightTangent = tangentDist / isectPointToLightCenterDist;
		float conePdf = ConePdf(cosThetaIsectPointLightTangent);
		float lightContr = 0.0f;
		int numSamplesInShadow = 0;
		for (int i = 0; i <= numOcclusionStepsPerLighti; i++)
		{
			for (int j = 0; j <= numOcclusionStepsPerLightj; j++)
			{
				// Generate current sample numbers used for sampling the cone.
				// Note: lerping (mix in GLSL) shrinks the domain of the sample numbers from e.g.
				// [0, 1] -> [0.25, 0.75]. This will reduce the spread of the samples, resulting
				// in more samples taken within a smaller domain. This should yield better AO.
				vec2 samplePoints = mix(lerpMin, lerpMax, vec2(float(i) / float(numOcclusionStepsPerLighti), float(j) / float(numOcclusionStepsPerLightj)));
				vec3 occlusionRayDir = SampleCone(isectPointToLightCenterDir, samplePoints, cosThetaIsectPointLightTangent);
				// Trace
				Ray occlusionRay = GenerateRay(isectPoint + (isectNormal * 0.001f), occlusionRayDir);
				traceNV(scene, rayFlags, cullMask, SECONDARY_CHIT_IDX, sbtRecordStride, SECONDARY_MISS_IDX, occlusionRay.origin, tMin, occlusionRay.dir, tMax, SECONDARY_PAYLOAD_LOCATION);
				
				// Intersect spherical light
				float lightDist = SphereIntersect(lightCenter, lightRadius, occlusionRay.origin, occlusionRay.dir, tMin, tMax);
				
				// Check for intersection with geometry before an intersection with the light:
				//    Either didn't hit some geometry or the distance to the geometry isect is
				//    further than the distance to the isect on the light
				if ((secondaryPayload.hitDist.x < 0.0f || secondaryPayload.hitDist.x >= lightDist))
				{
					lightContr += dot(isectNormal, occlusionRay.dir);
				}
				else
				{
					numSamplesInShadow++;
				}
			}
		}
		
		// Add light's contribution
		lightContr = lightContr / conePdf;
		color += lightEmittance * lightContr;
		shadow += lightEmittance * (totalOcclusionStepsPerLight - numSamplesInShadow) / totalOcclusionStepsPerLight;
	}
	color /= totalOcclusionSteps;
	color *= primaryPayload.materialColor.rgb;
	shadow /= totalOcclusionSteps;
	
	// Store
    imageStore(resultImage, ivec2(gl_LaunchIDNV.xy), vec4(color, 1.0f));
	imageStore(shadowImage, ivec2(gl_LaunchIDNV.xy), vec4(shadow.bgr, 0.0f));
    
    
    
    
    
    
    
    
    

#elif AO_HEMISPHERE
	vec3 color = vec3(0.0f);
	int numVisible = 0;
	for (int l = 0; l < otherData.numSphericalLightSources; l++)
	{
		vec3 lightCenter = lights[l].centerAndRadius.xyz;
		float lightRadius = lights[l].centerAndRadius.w;
		vec3 lightEmittance = lights[l].emittance.rgb;
		vec3 isectPointToLightCenter = lightCenter - isectPoint;
		vec3 isectPointToLightCenterDir = normalize(isectPointToLightCenter);
		float isectPointToLightCenterDist = length(isectPointToLightCenter);
		float isectPointToLightCenterClosestDist = isectPointToLightCenterDist - lightRadius;

		// Trace shadow rays
		Ray shadowRay = GenerateRay(isectPoint + (isectNormal * 0.001f), isectPointToLightCenterDir);
		traceNV(scene, rayFlags, cullMask, SECONDARY_CHIT_IDX, sbtRecordStride, SECONDARY_MISS_IDX, shadowRay.origin, tMin, shadowRay.dir, tMax, SECONDARY_PAYLOAD_LOCATION);
		
		// Check for intersection with geometry before an intersection with the light:
		//    Either didn't hit some geometry or
		//    the distance to the geometry isect is further than the distance to the isect on the light
		if ((secondaryPayload.hitDist.x < 0.0f || secondaryPayload.hitDist.x >= isectPointToLightCenterClosestDist))
		{
			numVisible++;
		}
		// https://en.wikipedia.org/wiki/Inverse-square_law
		color += (lightEmittance * (1.0f / isectPointToLightCenterClosestDist)) * dot(isectNormal, isectPointToLightCenterDir);
	}
	color /= otherData.numSphericalLightSources;
	color *= primaryPayload.materialColor.rgb;

	float occlusion = DEFAULT_OCCLUSION;
	if (numVisible < otherData.numSphericalLightSources)
	{
		int numOcclusionSteps = 7;
		int totalOcclusionSamples = numOcclusionSteps * numOcclusionSteps;
		// Sample hemisphere of directions given the normal at the intersection point
		int numAO = 0;
		float attenuation = 0.0f;
		for (int i = 0; i <= totalOcclusionSamples; i++)
		{
			// Sample occlusion ray direction and trace
			vec2 samplePoints = vec2(float(i % totalOcclusionSamples) / float(numOcclusionSteps), float(i) / float(totalOcclusionSamples));
			vec3 occlusionRayDir = SampleHemisphere(isectNormal, samplePoints);
			Ray occlusionRay = GenerateRay(isectPoint + (isectNormal * 0.001f), occlusionRayDir);
			traceNV(scene, rayFlags, cullMask, SECONDARY_CHIT_IDX, sbtRecordStride, SECONDARY_MISS_IDX, occlusionRay.origin, tMin, occlusionRay.dir, tMax, SECONDARY_PAYLOAD_LOCATION);
					
			// Check for intersection with geometry
			if (secondaryPayload.hitDist.x >= 0.0f)
			{
				attenuation += PolynomialAOAttenuation(secondaryPayload.hitDist.x);
				numAO++;
			}
			else
			{
				attenuation += POLYNONIAL_AO_ATTENUATION_Y_MAX;
			}
		}
		// Calculate occlusion
		occlusion = float(numAO) / float(totalOcclusionSamples);
		occlusion *= (1.0f - (float(numVisible) / float(otherData.numSphericalLightSources)));
		// Remember to scale totalOcclusionSamples with the maximum value that a single attenuation sample can give
		attenuation /= (float(totalOcclusionSamples) * POLYNONIAL_AO_ATTENUATION_Y_MAX);
		// Factor in attenuation
		occlusion *= attenuation;
		occlusion = min(occlusion, 1.0f);
	}
	
	// Store
    imageStore(resultImage, ivec2(gl_LaunchIDNV.xy), vec4(color, 1.0f));
    imageStore(shadowImage, ivec2(gl_LaunchIDNV.xy), vec4(occlusion, occlusion, occlusion, 0.0f));

#endif
}
